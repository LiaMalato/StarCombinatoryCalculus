import LeanProjeto2.FOLanguage
import LeanProjeto2.StarLanguage.FiniteTypes
import Init.Data.List.Basic
import Init.Data.List.Lemmas
import Mathlib.Data.Finset.Basic
import Mathlib.Data.Multiset.Basic
import Batteries

open LFormula
open LTerm
open Batteries

-- -------------------------------------------------------------
-- -------------------------------------------------------------
--             STAR LANGUAGE - THE SYNTAX
--            (SECTION 1.2.1: The syntax)
-- -------------------------------------------------------------
-- -------------------------------------------------------------

/- FILE DESCRIPTION:
In this file we discuss the syntax.
The file corresponds to pages 8 to 13.
-/

-- -------------------------------------------------------------
--                     TERMS (p.10-12)
-- -------------------------------------------------------------

-- -------------------------------------------------------------
-- DEFINITIONS 1.2 AND 1.3 (p.9-10):
-- Constants and terms of L^Ï‰_*
-- -------------------------------------------------------------

-- DEFINITION: Terms of L^Ï‰_*
inductive Term
| lcons : String â†’ Term                 -- L-constants: constants from L
| lfun : String â†’ Term                  --              function symbols of L
| pi                                    -- combinators:     Î 
| sigma                                 --                  Î£
| sing                                  -- star constants:  ğ”°
| bUnion                                --                  âˆª (binary union)
| iUnion                                --                  âˆª (indexed union)
| var : String â†’ Term                   -- variables
| app : Term â†’ Term â†’ Term              -- application of terms
deriving Repr, DecidableEq

open Term

-- NOTATION: Notation for combinators and star constants
notation "Î â‚" => pi
notation "Î£â‚" => sigma
notation "ğ”°â‚" => sing
notation "âˆªâ‚" => bUnion
notation "iâˆªâ‚" => iUnion

-- NOTATION: Notation for the application of terms
notation tâ‚ "Â·" tâ‚‚ => app tâ‚ tâ‚‚

-- EXAMPLE: some terms (variables) to use in future examples
--namespace examples
def var_x := var "x"
def var_y := var "y"
def var_z := var "z"
-- end examples


-- --------------------------------
-- TUPLES OF TERMS
-- --------------------------------

-- Term tuples will be defined as lists of terms.
-- For example, if tâ‚ tâ‚‚ : Term, then [tâ‚, tâ‚‚] : List Term.

-- DEFINITION: We define a function in order to access any element in a tuple.
-- The use of Option allows to get some/none. This is useful for instance if a term tuple
-- has 2 elements and we ask for the (non-existing) third element. The output is then 'none'.
def getElement (n : Nat) (t : List Term) : Option Term :=
  List.get? t n

-- -------
-- EXAMPLES: a tuple of terms and accessing its elements
-- -------

--namespace examples
def exTermTuple : List Term := [var_x, var_y]  -- a tuple of terms (a list with the variables x and y)

#eval getElement 0 exTermTuple          -- Output: var_x (the first element of exTermTuple)
#eval getElement 1 exTermTuple          -- Output: var_y (the second element of exTermTuple)
#eval getElement 2 exTermTuple          -- Output: none (there is no third element in exTermTuple)
--end examples

-- ------------------------
-- NOTATION 1.4 (p. 12):
-- Term tuple applications
-- ------------------------

-- DEFINITION: Term tuple applications
@[simp]
def TermTupleApp : List Term â†’ List Term â†’ List Term
| [], _   => []         -- If the first tuple is empty, return an empty list
| _ , []  => []         -- If the second tuple is empty, return an empty list
| (t :: ts), qs =>      -- Nests the applications of terms as in the mathematical definition
  let appNested := qs.reverse.foldr (fun q acc => app acc q) t
  (appNested :: (TermTupleApp ts qs))

-- NOTATION: term tuple applications will be written using âŠ™
@[simp]
notation tâ‚ "âŠ™" tâ‚‚ => TermTupleApp tâ‚ tâ‚‚

-- --------------
-- TECHNICALITIES:
-- Auxilliary lemmas related to term tuple applications with empty tuples
-- --------------

@[simp]
lemma app_empty_list_fst (t : List Term) : (TermTupleApp [] t) = [] :=
by
  rw [TermTupleApp]

@[simp]
lemma app_empty_list_sec (t : List Term) : (TermTupleApp t []) = [] :=
by
  induction t
  case nil =>
    rw [TermTupleApp]
  case cons _ _ _ =>
    simp

@[simp]
lemma app_empty_lists : (TermTupleApp [] []) = [] :=
by
  rw [TermTupleApp]

-- --------
-- EXAMPLES: Applications between term tuples
-- --------

-- namespace examples
-- The tuple consisting of the variable 'x' is a term tuple
#check [var_x]
#eval [var_x]

-- Applying two empty tuples yields the empty tuple
#eval TermTupleApp ([] : List Term) ([] : List Term)

-- Term tuple applications
def ex_app1 : List Term := [var "t1", var "t2"] âŠ™ [var "q1", var "q2", var "q3"]
#eval ex_app1       -- Output: tâ‚qâ‚qâ‚‚qâ‚ƒ, tâ‚‚qâ‚qâ‚‚qâ‚ƒ

def ex_app2 : List Term := [var "t1", var "t2", var "t3"] âŠ™ [var "q1", var "q2", var "q3"]
#eval ex_app2       -- Output: tâ‚qâ‚qâ‚‚qâ‚ƒ, tâ‚‚qâ‚qâ‚‚qâ‚ƒ, tâ‚ƒqâ‚qâ‚‚qâ‚ƒ

def ex_app3 : List Term := [var "t1", var "t2"] âŠ™ [var "q1", var "q2", var "q3", var "q4"]
#eval ex_app3       -- Output: tâ‚qâ‚qâ‚‚qâ‚ƒqâ‚„, tâ‚‚qâ‚qâ‚‚qâ‚ƒqâ‚„
-- end examples


-- -------------------------------
-- TECHNICALITIES in order to use TermTupleApp directly with strings instead of terms
-- Functions to transform lists of one type into lists of other types.
-- -------------------------------

-- DEFINITION: Function to transform a list of Strings into a list of 'Term.var',
-- i.e. turning 'ts : List String' into 't : List Term', but more specifically each term in t has type 'Term.var'
@[simp]
def List.tt (lst : List String) : List Term :=
  lst.map Term.var

-- --------
-- EXAMPLE:
-- --------

-- namespace examples
#check "w"            -- Output: String
#check ["w"]          -- Output: List String
#check ["w"].tt       -- Output: List Term
#eval ["w"].tt        -- Output: "w" is a variable

#eval (["w"].tt)âŠ™(["w"].tt)     -- Output: term application 'wÂ·w'
-- end examples


-- --------------------------------
-- WELL-FORMED TERMS
-- --------------------------------

-- DEFINITION: well-formed terms
inductive Term_is_wellformed : List String â†’ Term â†’ Prop
| wf_lcons {xs k} : x âˆˆ xs â†’ Term_is_wellformed xs (lcons k)
| wf_lfun {xs f} : x âˆˆ xs â†’ Term_is_wellformed xs (lfun f)
| wf_pi {xs} : Term_is_wellformed xs pi
| wf_sigma {xs} : Term_is_wellformed xs sigma
| wf_sing {xs} : Term_is_wellformed xs sing
| wf_bUnion {xs} : Term_is_wellformed xs bUnion
| wf_iUnion {xs} : Term_is_wellformed xs iUnion
| wf_var {xs x} : x âˆˆ xs â†’ Term_is_wellformed xs (var x)
| wf_app {xs t1 t2} : Term_is_wellformed xs t1 â†’ Term_is_wellformed xs t2 â†’ Term_is_wellformed xs (app t1 t2)

-- DEFINITION: well-formed tuple of terms
-- If each term in the tuple is well-formed so is the whole tuple
inductive TermTuple_is_wellformed : List String â†’ List Term â†’ Prop
| wf_tuple : (âˆ€ t âˆˆ ts, Term_is_wellformed xs t) â†’ (TermTuple_is_wellformed xs ts)


-- --------------------------------
-- SUBTERMS
-- --------------------------------

namespace Term

-- DEFINITION: subterm of a term
inductive isSubterm : Term â†’ Term â†’ Prop
| refl (t : Term) :             isSubterm t t                                       -- Terms are always subterms of themselves
| app_left {tâ‚ tâ‚‚ t' : Term} :  isSubterm t' tâ‚ â†’ isSubterm t' (Term.app tâ‚ tâ‚‚)     -- Subterms of applications (LHS)
| app_right {tâ‚ tâ‚‚ t' : Term} : isSubterm t' tâ‚‚ â†’ isSubterm t' (Term.app tâ‚ tâ‚‚)     -- Subterms of applications (RHS)

open isSubterm

-- DEFINITION: subterms of tuple of terms
inductive isSubtermTuple : List Term â†’ List Term â†’ Prop
| empty_subtuple : isSubtermTuple [] []                           -- The empty tuple is a subtuple of itself
| rec_subtuple {tâ‚ tâ‚‚ : Term} {tsâ‚ tsâ‚‚ : List Term} :             -- Recursive definition for non-empty tuples
    isSubterm tâ‚ tâ‚‚ â†’
    isSubtermTuple tsâ‚ tsâ‚‚ â†’
    isSubtermTuple (tâ‚ :: tsâ‚) (tâ‚‚ :: tsâ‚‚)

-- -------
-- EXAMPLE: checking if the variable 'x' is a subterm of the term application xÂ·y
-- -------

example : isSubterm (var "x") (app (var "x") (var "y")) :=
  by
    have H := isSubterm.refl (var "x")
    exact app_left H


-- -------------------------------------
-- FREE VARIABLES FOR TERMS
-- -------------------------------------

-- DEFINITION: all the (free) variables of terms, it returns the set of (free) variables
def freevars : Term â†’ Finset String
| (lcons k)
| (lfun f)
| pi
| sigma
| sing
| bUnion
| iUnion => {}
| var x => {x}
| app tâ‚ tâ‚‚ => tâ‚.freevars âˆª tâ‚‚.freevars

-- DEFINITION: (free) variables in tuples of terms
def freevarsTuple (tuple : List Term) : Finset String :=
  (tuple.map freevars).foldl (fun acc fv => acc âˆª fv) âˆ…


-- ---------------------------------------
-- CLOSED TERMS
-- ---------------------------------------

-- DEFINITION: closed terms and closed tuple terms
def isClosedTerm (t : Term) : Prop := freevars t = {}
def isClosedTupleTerm (t : List Term) : Prop := freevarsTuple t = {}


-- ------------------------------------------------------
-- TYPECHECKING THE TERMS OF L^{omega}_*
-- ------------------------------------------------------

/- DEFINITION: Term_TypeChecking a term
We typecheck the components of the formulas of L^Ï‰_*. This guarantees that the formulas have admissible types.
-/

def arities {n : Nat} : String â†’ Option Nat
| "f" => .some n
| _ => .none

def type_from_arity : Nat â†’ FType
| 0 => G
| (n + 1) => G âŸ¶ (type_from_arity n)

inductive Term_TypeChecking : Term â†’ FType â†’ Prop
| tcLcons {k}:          Term_TypeChecking (lcons k)  G
| tcLfun {f} {n}:       arities f = (.some n)  â†’ Term_TypeChecking (lfun f) (type_from_arity n)
| tcPi {Ïƒ Ï„}:           Term_TypeChecking pi        (Ïƒ âŸ¶ (Ï„ âŸ¶ Ïƒ))                                 -- Î _{Ïƒ,Ï„} : Ïƒ âŸ¶ (Ï„ âŸ¶ Ïƒ)
| tcSigma {Ïƒ Ï„ Ï}:      Term_TypeChecking sigma     ((Ïƒ âŸ¶ (Ï„ âŸ¶ Ï)) âŸ¶ ((Ïƒ âŸ¶ Ï„) âŸ¶ (Ïƒ âŸ¶ Ï)))      -- Î£_{Ïƒ,Ï„,Ï} : (Ïƒ âŸ¶ (Ï„ âŸ¶ Ï)) âŸ¶ ((Ïƒ âŸ¶ Ï„) âŸ¶ (Ïƒ âŸ¶ Ï))
| tcSing {Ïƒ}:           Term_TypeChecking sing      (Ïƒ âŸ¶ Ïƒâ‹†)                                       -- ğ”°_{Ïƒ} : Ïƒâ‹†
| tcBUnion {Ïƒ}:         Term_TypeChecking bUnion    (Ïƒâ‹† âŸ¶ (Ïƒâ‹† âŸ¶ Ïƒâ‹†))                              -- âˆª_{Ïƒ} : Ïƒâ‹† âŸ¶ (Ïƒâ‹† âŸ¶ Ïƒâ‹†)
| tcIUnion {Ïƒ Ï„}:       Term_TypeChecking iUnion    (Ïƒâ‹† âŸ¶ ((Ïƒ âŸ¶ Ï„â‹†) âŸ¶ Ï„â‹†))                       -- iâˆª_{Ïƒ} : Ïƒâ‹† âŸ¶ ((Ïƒ âŸ¶ Ï„â‹†) âŸ¶ Ï„â‹†)
| tcVar {x Ïƒ}:          Term_TypeChecking (var x)   Ïƒ                                              -- Variables x : Ïƒ
| tcApp {tâ‚ tâ‚‚ Ïƒ Ï„}:    Term_TypeChecking tâ‚ (Ïƒ âŸ¶ Ï„) â†’
                        Term_TypeChecking tâ‚‚ Ïƒ â†’
                        Term_TypeChecking (app tâ‚ tâ‚‚) Ï„                                            -- If tâ‚ : (Ïƒ âŸ¶ Ï„) and tâ‚‚ : Ïƒ, then tâ‚tâ‚‚ : Ï„

-- DEFINITION: Term_TypeChecking for tuples of terms
inductive TermTuple_TypeChecking : List Term â†’ List FType â†’ Prop
| tcRecTuple {t : Term} {Ïƒ : FType} {ts : List Term} {Ïƒs : List FType} :
    Term_TypeChecking t Ïƒ â†’                             -- If the first term has the first type of the list of types
    TermTuple_TypeChecking ts Ïƒs â†’                      -- if the other terms in the tuple have the corresponding types in the list of types
    TermTuple_TypeChecking (t :: ts) (Ïƒ :: Ïƒs)          -- then the whole tuple typechecks.

open Term_TypeChecking


-- -------------------------------------
-- TERM SUBSTITUTION IN L^Ï‰_*
-- -------------------------------------

-- DEFINITION: term substitution, we replace 'x' by 'replacement' in some term 't' (lcons, var, app, rest)
def term_substitution (x : String) (replacement : Term) : Term â†’ Term
| lcons k   => lcons k
| lfun f    => lfun f
| var y     => if x = y
               then replacement
               else (var y)
| app tâ‚ tâ‚‚ => app (term_substitution x replacement tâ‚) (term_substitution x replacement tâ‚‚)  -- In an application, we do the substitution in each term
| t => t                                                                                      -- The combinators Î , Î£ and the star constants ğ”°, âˆª, â‹ƒ are constants and hence are not affected by substitution

-- DEFINITION: term substitution for term tuples, we substitute 'x' by 'replacement' in some term tuple 't'
def term_substitutionTuple (x : String) (replacement : Term) : List Term â†’ List Term
| [] => []
| (t :: ts) => (term_substitution x replacement t) :: term_substitutionTuple x replacement ts

-- ---------
-- EXAMPLES: substitutions
-- ---------

-- EXAMPLE: term substitution
#eval term_substitution "x" Î â‚ (var_x)                                -- Replacing x by Î â‚ in x gives Î â‚
#eval term_substitution "x" Î â‚ (var_y)                                -- Replacing x by Î â‚ in y gives y
#eval term_substitution "x" âˆªâ‚ (((var_x)Â·(var_y))Â·(var_z))            -- Replacing x by âˆªâ‚ in (xÂ·y)Â·z gives (âˆªâ‚Â·y)Â·z

-- EXAMPLE: term tuple substitution
-- We substitute "x" by an lconstant a in the tuple [x, (yÂ·z)]
#eval term_substitutionTuple "x" (lcons "c") [var_x, (Î â‚Â·var_y)]       -- Output: [c, (Î â‚Â·z)]

end Term


-- -------------------------------------------------------------
--                     FORMULAS (p.12-14)
-- -------------------------------------------------------------

/-
We define the formulas of L^Ï‰_*:
  a) The atomic formulas (Definition 1.6 - p.11)
  b) The base formulas (Definition 1.10 - p.14)
  c) The formulas (Definition 1.7 - p.13)
-/

-- -------------------------------------------------------------
-- DEFINITION 1.7 (p.13):
-- Formulas of L^Ï‰_*
-- -------------------------------------------------------------

inductive Formula : Type
| rel:         String â†’ List Term â†’ Formula             -- R(tâ‚, ..., tâ‚™) with R relational symbol of L and tâ‚,...,tâ‚™ ground terms in L^Ï‰_*
| eq:          Term â†’ Term â†’ Formula                    -- t =Ïƒ q
| mem:         Term â†’ Term â†’ Formula                    -- t âˆˆÏƒ q
| not:         Formula â†’ Formula                        -- If A is a formula, then so is (Â¬A)
| or:          Formula â†’ Formula â†’ Formula              -- If A and B are formulas, then so is (Aâˆ¨B)
| unbForall:   String â†’ Formula â†’ Formula               -- If A is a formula, then so is (âˆ€x A)
| bForall:     String â†’ Term â†’ Formula â†’ Formula        -- If A is a formula, then so is (âˆ€xâˆˆt A)
deriving Repr

open Formula


-- ------------------------------------
-- UNIVERSAL QUANTIFICATIONS FOR TUPLES
-- ------------------------------------

-- DEFINITION: âˆ€ (unbounded) for tuples
-- We convert the list of variables into a nested sequence of âˆ€ quantifiers
def unbForallTuple (vars : List String) (A : Formula) : Formula :=
  vars.foldr (fun v acc => Formula.unbForall v acc) A

-- DEFINITION: âˆ€ (bounded) for tuples
def bForallTuple (vars : List String) (terms : List Term) (A : Formula) : Formula :=
  let applyBForall := List.zip vars terms                             -- Applies bForall using the variable and corresponding term.
  applyBForall.foldr (fun (v, t) acc => Formula.bForall v t acc) A    -- Folds over the list of (variable, term) pairs.
                                                                      -- This is done by applying bForall and this in the correct order.

-- TECHNICAL DEFINITION:
-- If the lists are empty, then folding over them, gives just the initial formula 'A'.
@[simp]
theorem bForallTuple_nil (A : Formula) :
  bForallTuple [] ([].tt) A = A :=
by
  unfold bForallTuple
  simp

open Formula


-- ---------------------------
-- NOTATION: Primitive symbols
-- ---------------------------

-- Here we introduce notation to get closer to the mathematical definition.
-- To avoid clashes with the core notations already present in Lean, we add 'â‚' to each symbol in L^Ï‰_*.

-- NOTATION: Notation for the equality and the membership symbols
notation tâ‚ "=â‚" tâ‚‚ => Formula.eq tâ‚ tâ‚‚
notation tâ‚ "âˆˆâ‚" tâ‚‚ => Formula.mem tâ‚ tâ‚‚

-- NOTATION: Notation for the primitive symbols Â¬, âˆ¨, âˆ€x and âˆ€xâˆˆt in L^Ï‰_*
notation "Â¬â‚" A   => not A
notation A "âˆ¨â‚" B => or A B
notation "âˆ€â‚â‚"    => unbForall
notation "bâˆ€â‚â‚"   => bForall
notation "âˆ€â‚"     => unbForallTuple   -- unbounded universal quantifier for tuples
notation "bâˆ€â‚"    => bForallTuple     -- bounded universal quantifier for tuples


-- -----------------------------------------------------------------------
-- DEFINITION 1.8 (p.14):
-- The bounded and unbounded existential quantifiers âˆƒxâˆˆt (defined symbol)
-- -----------------------------------------------------------------------

-- DEFINITION: The unbounded existential quantifier (single variable):          âˆƒx A := Â¬ (âˆ€x (Â¬ A))
@[simp]
def unbExists (x : String) (A : Formula) : Formula :=
  Â¬â‚(âˆ€â‚â‚ x (Â¬â‚ A))

-- DEFINITION: The bounded existential quantifier (single):                     âˆƒxâˆˆt A := Â¬ (âˆ€xâˆˆt (Â¬ A))
@[simp]
def bExists (x : String) (t : Term) (A : Formula) : Formula :=
  Â¬â‚(bâˆ€â‚â‚ x t (Â¬â‚ A))

-- DEFINITION: The unbounded existential quantifier (for tuples of variables):  âˆƒx A := Â¬ (âˆ€x (Â¬ A))
@[simp]
def unbExistsTuple (x : List String) (A : Formula) : Formula :=
  Â¬â‚ (âˆ€â‚ x (Â¬â‚A))

-- DEFINITION: The bounded existential quantifier (for tuples):                 âˆƒxâˆˆt A := Â¬ (âˆ€xâˆˆt (Â¬ A))
@[simp]
def bExistsTuple (x : List String) (t : List Term) (A : Formula) : Formula :=
  Â¬â‚ (bâˆ€â‚ x t (Â¬â‚A))

-- NOTATION: bounded and unbounded existential quantifiers
notation "âˆƒâ‚â‚" => unbExists
notation "bâˆƒâ‚â‚" => bExists
notation "âˆƒâ‚" => unbExistsTuple
notation "bâˆƒâ‚" => bExistsTuple


-- --------------------------------------------------------------------
-- DEFINITION and NOTATION: the rest of the defined symbols (p.9):
-- Usual logical abbreviations for the defined symbols âˆ§, â†’, â†” in L^Ï‰_*
-- --------------------------------------------------------------------

-- DEFINITION: Conjunction:  A âˆ§ B := Â¬(Â¬Aâˆ¨Â¬B)
@[simp]
def F_and (A B : Formula) : Formula :=
  Â¬â‚ ((Â¬â‚ A) âˆ¨â‚ (Â¬â‚ B))

notation A "âˆ§â‚" B => F_and A B

-- DEFINITION: Implication:  A â†’ B := Â¬ A âˆ¨ B
@[simp]
def F_implies (A B : Formula) : Formula :=
  (Â¬â‚ A) âˆ¨â‚ B

notation A "â†’â‚" B => F_implies A B

-- DEFINITION: Equivalence:  A â†” B := (A â†’ B) âˆ§ (B â†’ A)
@[simp]
def F_iff (A B : Formula) : Formula :=
  Â¬â‚ ((Â¬â‚((Â¬â‚A) âˆ¨â‚ B)) âˆ¨â‚ (Â¬â‚((Â¬â‚B) âˆ¨â‚ A)))
  -- (A â†’â‚ B) âˆ§â‚ (B â†’â‚ A)
  -- (Â¬â‚ A âˆ¨â‚ B) âˆ§â‚ (Â¬â‚ B âˆ¨â‚ A)

notation A "â†”â‚" B => F_iff A B


-- ------------------------------------------------------
-- DEFINITION: Membership and equality symbols for tuples
-- ------------------------------------------------------

-- DEFINITION: Membership symbol (tuples):
-- Function that takes two lists of terms and applies 'mem', âˆˆ, pairwise to corresponding elements of the lists.
def mem_tuple : List Term â†’ List Term â†’ Formula
| [], [] => Formula.rel "True" []                         -- When the lists are empty
| (t1::ts), (q1::qs) => (t1 âˆˆâ‚ q1) âˆ§â‚ (mem_tuple ts qs)   -- Recursively creates pairwise âˆˆ
| _, _ => Formula.rel "False" []                          -- Other cases (for example, when the lists have different lengths)

notation ltâ‚ "âˆˆ_t" ltâ‚‚ => mem_tuple ltâ‚ ltâ‚‚

-- DEFINITION: Equality symbol (tuples):
-- Function that takes two lists of terms and applies 'eq', =, pairwise to corresponding elements of the lists.
def eq_tuple : List Term â†’ List Term â†’ Formula
| [], [] => Formula.rel "True" []                         -- When the lists are empty
| (t1::ts), (q1::qs) => (t1 =â‚ q1) âˆ§â‚ (eq_tuple ts qs)    -- Recursively creates pairwise =
| _, _ => Formula.rel "False" []                          -- Other cases (for example, when the lists have different lengths)

notation ltâ‚ "=_t" ltâ‚‚ => eq_tuple ltâ‚ ltâ‚‚


-- -----------------------------
-- WELL-FORMED FORMULAS IN L^Ï‰_*
-- -----------------------------

inductive Formula_is_wellformed : List String â†’ Formula â†’ Prop
| wf_rel {xs R ts} :
    (âˆ€ t âˆˆ ts, Term_is_wellformed xs t) â†’
    Formula_is_wellformed xs (rel R ts)           -- If tâ‚,...,tâ‚™ are well-formed terms, then so is R(tâ‚,...,tâ‚™).
| wf_eq {xs tâ‚ tâ‚‚} :
    Term_is_wellformed xs tâ‚ â†’
    Term_is_wellformed xs tâ‚‚ â†’
    Formula_is_wellformed xs (eq tâ‚ tâ‚‚)           -- If tâ‚ and tâ‚‚ are well-formed, then so is tâ‚=tâ‚‚.
| wf_mem {xs tâ‚ tâ‚‚} :
    Term_is_wellformed xs tâ‚ â†’
    Term_is_wellformed xs tâ‚‚ â†’
    Formula_is_wellformed xs (mem tâ‚ tâ‚‚)          -- If tâ‚ and tâ‚‚ are well-formed, then so is tâ‚âˆˆtâ‚‚.
| wf_not {xs A} :
    Formula_is_wellformed xs A â†’
    Formula_is_wellformed xs (not A)              -- If A is a well-formed formula, then so is Â¬A.
| wf_or {xs A B} :
    Formula_is_wellformed xs A â†’
    Formula_is_wellformed xs B â†’
    Formula_is_wellformed xs (or A B)             -- If A and B are well-formed formulas, then so is Aâˆ¨B.
| wf_unbForall {xs x A} :
    Formula_is_wellformed (x :: xs) A â†’
    Formula_is_wellformed xs (unbForall x A)      -- If A is a well-formed formula (for our list xs and the bound variable x), then so is âˆ€x A.
| wf_bForall {xs x t A} :
    Formula_is_wellformed (x :: xs) A â†’
    Formula_is_wellformed xs (bForall x t A)      -- Analogously for âˆ€xâˆˆt A


-- -------------------------------------
-- FREE VARIABLES FOR FORMULAS IN L^Ï‰_*
-- -------------------------------------

-- DEFINITION: the free variables of a formula
def Formula.freevars : Formula â†’ Finset String
| .rel _ ts       => Finset.fold (fun x y => x âˆª y) {} Term.freevars ts.toFinset
| .eq tâ‚ tâ‚‚
| .mem tâ‚ tâ‚‚      => tâ‚.freevars âˆª tâ‚‚.freevars
| .or A B         => A.freevars âˆª B.freevars
| .not A          => A.freevars
| .unbForall x A
| .bForall x t A  => A.freevars \ ([x].toFinset)

-- DEFINITION: all the variables of a formula
def Formula.allvars : Formula â†’ Finset String
| .rel _ ts       => Finset.fold (fun x y => x âˆª y) {} Term.freevars ts.toFinset    -- All the variables from the list of terms used in the predicate.
| .eq tâ‚ tâ‚‚
| .mem tâ‚ tâ‚‚      => tâ‚.freevars âˆª tâ‚‚.freevars                                      -- For both terms, we collect the variables from both and consider the union of those sets.
| .or A B         => A.allvars âˆª B.allvars                                          -- Take the variables from both subformulas and consider the union of those sets.
| .not A          => A.allvars                                                      -- The variables of Â¬A are the ones of A.
| .unbForall x A  => A.allvars âˆª {x}                                                -- The variables of âˆ€x A are the ones of A together with the bound variable.
| .bForall x t A  => t.freevars âˆª A.allvars âˆª {x}                                   -- The variables of âˆ€xâˆˆt A are the ones of A together with the bound variable and the variables in t.

/-
With these definitions, we can create assumptions such as:
    x is a free variable of formula A       -> In Lean: x âˆˆ A.freevars
    x is not a free variable of formula A   -> In Lean: x âˆ‰ A.freevars

    x is a variable of formula A            -> In Lean: x âˆˆ A.allvars
    x is not a variable of formula A        -> In Lean: x âˆ‰ A.allvars
-/


-- ----------------------------
-- SENTENCES (CLOSED FORMULAS)
-- ----------------------------

def isClosed (A : Formula) : Prop := Formula.freevars A = {}
def isClosed_check (A : Formula) : Bool := (Formula.freevars A) = {}       -- Prints true or false depending on if there are free variables or not

-- ---------
-- EXAMPLES: Free variables and checks whether it is a sentence
-- ---------

-- EXAMPLE 1: Formula R(x,y) âˆ¨ (âˆ€z Q(z))
def ex1_freevars_formula := (rel "R" [var "x", var "y"]) âˆ¨â‚ (âˆ€â‚â‚ "z" (rel "Q" [var "z"]))
#eval Formula.freevars ex1_freevars_formula                         -- The set of free variables is the set {x,y}, that is {"x", "y"}.
#eval isClosed_check ex1_freevars_formula                           -- Since ex1_freevars_formula has x and y as free variables, the output is false.

-- EXAMPLE 2: Formula R(x,y)
def ex2_freevars_formula := (rel "R" [var "x", var "y"])
#eval Formula.freevars ex2_freevars_formula                         -- The set of free variables is the set {x,y}, that is {"x", "y"}.
#eval isClosed_check ex2_freevars_formula                           -- Since ex2_freevars_formula has x and y as free variables, the output is false.

-- EXAMPLE 3: Formula R(x,y)
def ex3_freevars_formula := (âˆ€â‚â‚ "x" (rel "R" [var "x"]))
#eval Formula.freevars ex3_freevars_formula                         -- There are no free variables, the set of free variables is âˆ….
#eval isClosed_check ex3_freevars_formula                           -- Since ex3_freevars_formula has no free variables, the output is true.


-- ------------------------------------------------------
-- ATOMIC AND BASE FORMULAS
-- ------------------------------------------------------

-- DEFINITION: Outputs a proposition stating that a formula is atomic:
inductive isAtomic : Formula â†’ Prop
| at_rel (R : String) (l_term : List Term): isAtomic (rel R l_term)             -- Formulas of the form R(tâ‚,...,tn) are atomic
| at_eq (tâ‚ tâ‚‚ : Term) : isAtomic (eq tâ‚ tâ‚‚)                                    -- Formulas of the form tâ‚âˆˆtâ‚‚ are atomic
| at_mem (tâ‚ tâ‚‚ : Term) : isAtomic (mem tâ‚ tâ‚‚)                                  -- Formulas of the form tâ‚=tâ‚‚ are atomic

-- DEFINITION: Outputs a proposition stating that a formula is base:
inductive isBase : Formula â†’ Prop
| b_atom : isAtomic A â†’ isBase A                                                -- Atomic formulas are base formulas
| b_not (h: isBase A) : isBase (not A)                                          -- If A is base, then so is Â¬â‚A
| b_or (h1: isBase A) (h2 : isBase B) : isBase (or A B)                         -- If A and B are base, then so is Aâˆ¨â‚B
| b_bForall (x : String) (t : Term) (h : isBase A) : isBase (bForall x t A)     -- If A is base, then so is âˆ€xâˆˆt A

open isAtomic
open isBase

-- ---------
-- EXAMPLES:
-- ---------

-- EXAMPLE 1: The formula p=q is atomic
example (p q : Term) : isAtomic (p =â‚ q) := by
  exact at_eq p q

-- EXAMPLE 2: Let A be an atomic formula (assumption h), then it is base.
example (A : Formula) (h : isAtomic A)  : (isBase A) := by
  exact b_atom h

-- EXAMPLE 3: If A is an atomic formula and B is a base formula, then Aâˆ¨B is base.
example (A B : Formula) (hA_at : isAtomic A) (hB_b : isBase B) : (isBase (Aâˆ¨â‚B)) := by
  have h := b_atom hA_at
  exact b_or h hB_b

-- EXAMPLE 4: The formula x=y is a base formula.
example (x y : String) : isBase ((var x) =â‚ (var y)) := by exact b_atom (isAtomic.at_eq (var x) (var y))

-- ---------------------------------------------------------


-- -----------------------------------------------------------------------
-- REMARK 1.10 (p.14):
-- Defined symbols of base formulas are also base
-- -----------------------------------------------------------------------

-- LEMMA: Conj_base states that if A and B are base formulas, then so is their conjunction A âˆ§ B
lemma Conj_base (A B : Formula) (hA : isBase A) (hB : isBase B) : (isBase (Aâˆ§â‚B)) := by
  unfold F_and
  exact b_not (b_or (b_not hA) (b_not hB))

-- LEMMA: Imp_base states that if A and B are base formulas, then so is their implication A â†’ B
lemma Imp_base (A B : Formula) (hA : isBase A) (hB : isBase B) : (isBase (Aâ†’â‚B)) := by
  unfold F_implies
  have h_nA := b_not hA
  exact b_or h_nA hB

-- LEMMA: Iff_base states that if A and B are base formulas, then so is their equivalence A â†” B
lemma Iff_base (A B : Formula) (hA : isBase A) (hB : isBase B) : (isBase (Aâ†”â‚B)) := by
  unfold F_iff
  have h_nA := b_not hA
  have h_nB := b_not hB
  have h_or_nAB := b_or h_nA hB
  have h_or_nBA := b_or h_nB hA
  have h_n_or_nAB := b_not h_or_nAB
  have h_n_or_nBA := b_not h_or_nBA
  have H := b_or h_n_or_nAB h_n_or_nBA
  exact b_not H

-- LEMMA: unbForall_base states that if A is a base formula, then so is âˆƒxâˆˆt A
lemma bExists_base {A : Formula} (x : String) (t : Term) (hA : isBase A) : (isBase (bâˆƒâ‚â‚ x t A)) := by
  unfold bExists
  have h_nA := b_not hA
  have h_unbForall_nA := b_bForall x t h_nA
  exact b_not h_unbForall_nA


-- ------------------
-- EXAMPLE 1.6 (p.14)
-- ------------------

-- EXAMPLE 1.6.1 (p.14): If B is a base formula, then so is âˆ€xâˆˆt B(x)
example (B : Formula) (hB_b : isBase B) (x : String) (t : Term): (isBase (bâˆ€â‚â‚ x t (Â¬â‚ B))) := by
  have H := b_not hB_b
  exact b_bForall x t H

-- EXAMPLE 1.6.2 (p.14): If A and B are base formulas, then so is âˆ€xâˆˆt âˆƒyâˆˆq (Aâˆ¨B)
example (A B : Formula) (hA_b : isBase A) (hB_b : isBase B) (x y : String) (t q : Term): (isBase (bâˆ€â‚â‚ x t (bâˆƒâ‚â‚ y q (A âˆ¨â‚ B)))) := by
  have H_or_AB := b_or hA_b hB_b
  have H_bExists := bExists_base y q H_or_AB
  exact b_bForall x t H_bExists


-- -------------------------------------
-- FORMULA SUBSTITUTION IN L^Ï‰_*
-- -------------------------------------

mutual
  def List.subst (ts : List Term) (substitutions : HashMap String Term) : List Term :=
  ts.map (fun t => Term.subst t substitutions)

  def Term.subst (t:Term) (substitutions:HashMap String Term) : Term :=
  match t with
  | lcons k => substitutions.findD k (lcons k)
  | lfun f => substitutions.findD f (lfun f)
  | pi => pi
  | sigma => sigma
  | sing => sing
  | bUnion => bUnion
  | iUnion => iUnion
  | var n => substitutions.findD n (var n)
  | app tâ‚ tâ‚‚ => app (tâ‚.subst substitutions) (tâ‚‚.subst substitutions)
end

def Formula.subst (A:Formula) (substitutions : HashMap String Term) : Formula :=
match A with
| rel s ts => rel s (ts.map (fun t => Term.subst t substitutions))    -- for lists of terms, this is enough
| eq t1 t2 => eq (t1.subst substitutions) (t2.subst substitutions)
| mem t1 t2 => mem (t1.subst substitutions) (t2.subst substitutions)
| not A' => not (A'.subst substitutions)
| or A1 A2 => or (A1.subst substitutions) (A2.subst substitutions)
| unbForall s A' => match substitutions.find? s with
                    | .none => unbForall s (A'.subst substitutions)
                    | .some _ => unbForall s A'
| bForall s t A' => match substitutions.find? s with
                    | .none => bForall s (t.subst substitutions) (A'.subst substitutions)
                    | .some _ => bForall s (t.subst substitutions) A'

def Formula.term_subst (A:Formula) (tr1 tr2:Term) : Formula :=
match A with
| rel s ts => rel s (ts.map (fun t => if t = tr1 then tr2 else t))    -- for lists of terms, this is enough
| eq t1 t2 => eq (if t1 = tr1 then tr2 else t1) (if t2 = tr1 then tr2 else t2)
| mem t1 t2 => mem (if t1 = tr1 then tr2 else t1) (if t2 = tr1 then tr2 else t2)
| not A' => not (A'.term_subst tr1 tr2)
| or A1 A2 => or (A1.term_subst tr1 tr2) (A2.term_subst tr1 tr2)
| unbForall s A' => unbForall s (A'.term_subst tr1 tr2)
| bForall s t A' => bForall s (if t = tr1 then tr2 else t) (A'.term_subst tr1 tr2)

-- -------------------------------------
-- FORMULA SUBSTITUTION IN L^Ï‰_*
-- -------------------------------------

def substitution_formula (x : String) (replacement : Term) : Formula â†’ Formula
-- | (L_Form A) => match replacement with
--               | .lcons r => L_Form (Lsubstitution_formula x r A)
--               | _ => (L_Form A)
| (rel R terms) => rel R (terms.map (term_substitution x replacement))
| (tâ‚ =â‚ tâ‚‚) => (term_substitution x replacement tâ‚) =â‚ (term_substitution x replacement tâ‚‚)
| (tâ‚ âˆˆâ‚ tâ‚‚) => (term_substitution x replacement tâ‚) âˆˆâ‚ (term_substitution x replacement tâ‚‚)
| (Formula.not A) => Â¬â‚ (substitution_formula x replacement A)                                                       -- recursivamente em A
| (Formula.or A B) => (substitution_formula x replacement A) âˆ¨â‚ (substitution_formula x replacement B)               -- recursivamente em A e B
| (âˆ€â‚â‚ y A) => if x = y then âˆ€â‚â‚ y A
              else âˆ€â‚â‚ y (substitution_formula x replacement A)
| (bâˆ€â‚â‚ y t A) => if x = y then bâˆ€â‚â‚ y t A
              else (bâˆ€â‚â‚ y t (substitution_formula x replacement A))

-- ---------
-- EXAMPLES: formula substitution
-- ---------

-- EXAMPLE 1: substituting x by Î  in R(x,y)
#eval substitution_formula "x" Î â‚ (rel "R" [var_x, var_y])        -- Output: R(Î â‚,y)

-- EXAMPLE 2: substituting z by âˆª in (Â¬(xâˆˆy))âˆ¨(z=w)
def ex2_subst : Formula :=
    (Â¬â‚( (var "x")âˆˆâ‚(var "y") )) âˆ¨â‚ ((var "z")=â‚(var "w"))
#eval substitution_formula "z" âˆªâ‚ ex2_subst                       -- Output: (Â¬â‚(x âˆˆâ‚ y)) âˆ¨â‚ (âˆªâ‚ =â‚ w)


-- ------------------------------------------------------
-- TYPECHECKING
-- We typecheck the components of the formulas of L^Ï‰_*.
-- This guarantees that the formulas have admissible types.
-- ------------------------------------------------------

inductive Formula_TypeChecking : Formula â†’ Prop
| tcRel {R l_terms} :                                               -- If R is a relational symbol,
    (âˆ€ t, t âˆˆ l_terms â†’ Term_TypeChecking t G) â†’                    -- if we apply R to a list l_terms of ground terms,
    Formula_TypeChecking (Formula.rel R l_terms)                    -- then R(tâ‚,...,tâ‚™) typechecks.
| tcEq {Ïƒ tâ‚ tâ‚‚} :
    Term_TypeChecking tâ‚ Ïƒ â†’
    Term_TypeChecking tâ‚‚ Ïƒ â†’
    Formula_TypeChecking (Formula.eq tâ‚ tâ‚‚)                         -- If tâ‚ and tâ‚‚ have the same type, then the formula tâ‚=tâ‚‚ type checks.
| tcMem {Ïƒ tâ‚ tâ‚‚} :
    Term_TypeChecking tâ‚ Ïƒ â†’                                        -- If tâ‚:Ïƒ
    Term_TypeChecking tâ‚‚ (Ïƒâ‹†) â†’                                     -- and if tâ‚‚:Ïƒ*,
    Formula_TypeChecking (Formula.mem tâ‚ tâ‚‚)                        -- then the formula tâ‚âˆˆtâ‚‚ type checks.
| tcNot {A} :
    Formula_TypeChecking A â†’ Formula_TypeChecking (Formula.not A)   -- If formula A type checks, then so does Â¬A.
| tcOr {A B} :
    Formula_TypeChecking A â†’
    Formula_TypeChecking B â†’
    Formula_TypeChecking (Formula.or A B)                           -- If formulas A and B type check, then so does Aâˆ¨B.
| tcbForall {x Ïƒ t A} :
    Term_TypeChecking (Term.var x) Ïƒ â†’                              -- If x:Ïƒ,
    Term_TypeChecking t (Ïƒâ‹†) â†’                                      -- if t:Ïƒ*
    Formula_TypeChecking A â†’                                        -- and if the formula A type checks,
    Formula_TypeChecking (Formula.bForall x t A)                    -- then so does âˆ€xâˆˆt A.
| tcunbForall {x Ïƒ A} :
    Term_TypeChecking (Term.var x) Ïƒ â†’                              -- If x:Ïƒ
    Formula_TypeChecking A â†’                                        -- and if the formula A type checks,
    Formula_TypeChecking (Formula.unbForall x A)                    -- then so does âˆ€x A.
